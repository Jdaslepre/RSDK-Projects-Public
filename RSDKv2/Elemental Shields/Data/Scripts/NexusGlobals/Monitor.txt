//----------------Sonic Nexus Monitor-----------------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias 10 : ANI_JUMPING
#alias 15 : TYPE_EXPLOSION

#alias Object.Value1 : Object.YVelocity

sub ObjectPlayerInteraction
	
	if Player.YVelocity>=0
		
		if Player.Animation==ANI_JUMPING
			if Object[2].PropertyValue!=0
				PlayerObjectCollision(C_TOUCH,-15,-16,15,16)
			else
				if Object[2].State==0
					PlayerObjectCollision(C_TOUCH,-15,-16,15,16)
				else
					PlayerObjectCollision(C_TOUCH,-39,-40,39,40)
				end if
			end if
			if CheckResult==1
				
				Object.State=0

				CreateTempObject(TYPE_EXPLOSION,0,Object.XPos,Object.YPos)
				Object[TempObjectPos].DrawOrder=4
				
				FlipSign(Player.YVelocity)
				
				//Change to Broken Monitor
				//(Where the action happens)
				Object.Type++
				Object.Priority=1
				
				//Setup The Starting point for
				//Power up that Pops out
				Object.Value0=Object.YPos
				Object.Value1=-229376

				PlaySfx(11,false)
			end if
			
		else
			PlayerObjectCollision(C_BOX,-15,-16,15,16)
		end if
		
	else
		PlayerObjectCollision(C_BOX,-15,-16,15,16)

		if CheckResult==4
			Object.State=1
			Object.YVelocity=-131072
		end if
	end if

	if Object.State==1

		Object.YVelocity+=14336
		Object.YPos+=Object.YVelocity

		if Object.YVelocity>=0
			ObjectTileCollision(0,0,16,0)

			if CheckResult==1
				Object.YVelocity=0
				Object.State=0
			end if
		end if

	end if
	
	Object.Value2++
	if Object.Value2>=32
		CheckResult=0

		if Object.PropertyValue==2
			CheckResult=1
		end if

		if Object.PropertyValue>=7
			CheckResult=1
		end if

		if CheckResult==1
			Rand(Object.Value2,4)
			switch Object.Value2
			case 0
				Object.PropertyValue=2
				break
			case 1
				Object.PropertyValue=7
				break
			case 2
				Object.PropertyValue=8
				break
			case 3
				Object.PropertyValue=9
				break
			end switch
		end if
	end if
end sub

sub ObjectDraw
	
	if Object.Value0<2
		Rand(Object.Value0,16)
	else
		Object.Value0--
	end if
	
	switch Object.Value0
	case 4
	case 5
		DrawSprite(0)
		break
		
		default
		DrawSprite(Object.PropertyValue)
	end switch
end sub

sub ObjectStartup
	LoadSpriteSheet("NexusGlobals/Items.gif")
	
	//Monitor Frames
	SpriteFrame(-16,-16,32,32,18,1)
	SpriteFrame(-16,-16,32,32,18,34)
	SpriteFrame(-16,-16,32,32,18,67)
	SpriteFrame(-16,-16,32,32,18,100)
	SpriteFrame(-16,-16,32,32,18,133)
	SpriteFrame(-16,-16,32,32,18,166)
	SpriteFrame(-16,-16,32,32,18,199)
	SpriteFrame(-16,-16,32,32,18,232)
	SpriteFrame(-16,-16,32,32,18,265)
	SpriteFrame(-16,-16,32,32,18,298)
	
end sub

sub RSDK
	LoadSpriteSheet("NexusGlobals/Items.gif")
	SetEditorIcon(Icon0,0,-16,-16,32,32,18,1)
	SetEditorIcon(Icon1,0,-16,-16,32,32,18,34)
	SetEditorIcon(Icon2,0,-16,-16,32,32,18,67)
	SetEditorIcon(Icon3,0,-16,-16,32,32,18,100)
	SetEditorIcon(Icon4,0,-16,-16,32,32,18,133)
	SetEditorIcon(Icon5,0,-16,-16,32,32,18,166)
	SetEditorIcon(Icon6,0,-16,-16,32,32,18,199)
end sub